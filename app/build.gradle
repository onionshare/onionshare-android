plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.1'
}

static def versionCodeEpoch() {
    return (new Date().getTime() / 1000).toInteger()
}

def gitCommit = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short=7', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdk 33

    defaultConfig {
        namespace "org.onionshare.android"

        applicationId "org.onionshare.android"
        minSdk 21
        targetSdk 33
        versionCode 15
        versionName "0.1.14-alpha"

        vectorDrawables {
            useSupportLibrary true
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        benchmark {
            signingConfig signingConfigs.debug
            debuggable false
        }
    }
    flavorDimensions "releaseType"
    productFlavors {
        stable {
            dimension "releaseType"
        }
        nightly {
            dimension "releaseType"
            applicationIdSuffix ".nightly"
            versionCode versionCodeEpoch()
            versionNameSuffix " ($gitCommit)"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.6'
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
        exclude 'META-INF/*'
        // Due to https://github.com/Kotlin/kotlinx.coroutines/issues/2023
        exclude 'META-INF/licenses/*'
        exclude '**/attach_hotspot_windows.dll'
    }
    lintOptions {
        disable 'MissingTranslation', 'MissingQuantity'
    }
}

configurations {
    tor
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'com.google.android.material:material:1.9.0'

    implementation platform("androidx.compose:compose-bom:$compose_version")
    implementation 'androidx.compose.material:material'
    implementation "androidx.compose.material:material-icons-extended"
    implementation 'androidx.compose.ui:ui-tooling-preview'

    implementation "androidx.navigation:navigation-compose:2.6.0"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "androidx.profileinstaller:profileinstaller:1.3.1"
    implementation "androidx.security:security-crypto:1.1.0-alpha06"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Tor
    implementation "org.briarproject:onionwrapper-android:0.0.5"
    tor 'org.briarproject:tor-android:0.4.7.14'
    tor 'org.briarproject:obfs4proxy-android:0.0.14-tor2'
    tor 'org.briarproject:snowflake-android:2.5.1'
    implementation 'org.briarproject:moat-api:0.2'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'

    def ktor_version = '2.3.1'
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-server-pebble:$ktor_version"
    implementation "io.ktor:ktor-server-status-pages:$ktor_version"
    implementation "io.ktor:ktor-server-call-logging:$ktor_version"
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'com.github.tony19:logback-android:3.0.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.5'
    testImplementation 'org.slf4j:slf4j-jdk14:2.0.7'
    testImplementation 'app.cash.turbine:turbine:1.0.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.4.3"
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'
}

def torLibsDir = 'src/main/jniLibs'

task cleanTorBinaries {
    outputs.dir torLibsDir
    doLast {
        delete fileTree(torLibsDir)
    }
}

clean.dependsOn cleanTorBinaries

task unpackTorBinaries {
    outputs.dir torLibsDir
    doLast {
        copy {
            from configurations.tor.collect { zipTree(it) }
            into torLibsDir
        }
    }
    dependsOn cleanTorBinaries
}
preBuild.dependsOn unpackTorBinaries

apply from: "${rootProject.rootDir}/gradle/ktlint.gradle"
