plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.1'
}

static def versionCodeEpoch() {
    return (new Date().getTime() / 1000).toInteger()
}

def gitCommit = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short=7', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdk 35

    defaultConfig {
        namespace "org.onionshare.android"

        applicationId "org.onionshare.android"
        minSdk 21
        targetSdk 35
        versionCode 22
        versionName "0.1.21-beta"

        vectorDrawables {
            useSupportLibrary true
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "releaseType"
    productFlavors {
        stable {
            dimension "releaseType"
        }
        fdroid {
            dimension "releaseType"
            applicationIdSuffix ".fdroid"
            // version codes get multiplied by 10 and an ABI suffix gets added to the code
            // if 'splitApk' property is set
        }
        nightly {
            dimension "releaseType"
            applicationIdSuffix ".nightly"
            versionCode versionCodeEpoch()
            versionNameSuffix " ($gitCommit)"
        }
    }
    splits {
        abi {
            // can not be defined per flavor, so we use a property to turn this on for F-Droid
            enable project.hasProperty('splitApk')
            reset() // Resets the list of ABIs to remove all included by default
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
        exclude 'META-INF/*'
        // Due to https://github.com/Kotlin/kotlinx.coroutines/issues/2023
        exclude 'META-INF/licenses/*'
        exclude '**/attach_hotspot_windows.dll'
    }
    lintOptions {
        disable 'MissingTranslation', 'ExtraTranslation', 'MissingQuantity'
    }
}

configurations {
    tor
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.activity:activity-compose:1.9.3'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation 'com.google.android.material:material:1.12.0'

    implementation platform("androidx.compose:compose-bom:$compose_version")
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.ui:ui-tooling-preview'

    implementation "androidx.navigation:navigation-compose:2.8.5"
    implementation "androidx.hilt:hilt-navigation-compose:1.2.0"
    implementation "androidx.profileinstaller:profileinstaller:1.4.1"
    implementation "androidx.security:security-crypto:1.1.0-alpha06"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Tor
    implementation "org.briarproject:onionwrapper-android:0.1.2"
    tor 'org.briarproject:tor-android:0.4.8.13'
    tor 'org.briarproject:lyrebird-android:0.5.0-2'
    implementation 'org.briarproject:moat-api:0.3'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    def ktor_version = '2.3.13'
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-server-pebble:$ktor_version"
    implementation "io.ktor:ktor-server-status-pages:$ktor_version"
    implementation "io.ktor:ktor-server-call-logging:$ktor_version"
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.slf4j:jul-to-slf4j:2.0.5'
    implementation 'com.github.tony19:logback-android:3.0.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.8'
    testImplementation 'org.slf4j:slf4j-jdk14:2.0.7'
    testImplementation 'app.cash.turbine:turbine:1.0.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.7.6"
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'
}

// Map for the version code that gives each ABI a value.
ext.abiCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, x86: 3, x86_64: 4]
// For each APK output variant, override versionCode with a combination of ext.abiCodes + variant.versionCode.
android.applicationVariants.configureEach { variant ->
    // Assigns a different version code for each output APK
    variant.outputs.each { output ->
        // Stores the value of ext.abiCodes that is associated with the ABI for this variant.
        def baseAbiVersionCode =
                // Determines the ABI for this variant and returns the mapped value.
                project.ext.abiCodes.get(output.getFilter(com.android.build.OutputFile.ABI))
        if (baseAbiVersionCode != null) {
            output.versionCodeOverride = 10 * variant.versionCode + baseAbiVersionCode
        }
        // leaves version code alone of there's no baseAbiVersionCode
    }
}

def torLibsDir = 'src/main/jniLibs'

tasks.register('cleanTorBinaries') {
    outputs.dir torLibsDir
    doLast {
        delete fileTree(torLibsDir)
    }
}

clean.dependsOn cleanTorBinaries

tasks.register('unpackTorBinaries') {
    outputs.dir torLibsDir
    doLast {
        copy {
            from configurations.tor.collect { zipTree(it) }
            into torLibsDir
        }
    }
    dependsOn cleanTorBinaries
}
preBuild.dependsOn unpackTorBinaries

apply from: "${rootProject.rootDir}/gradle/ktlint.gradle"
